/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


// isothermal Compressible Navier-Stokes
@kernel void advectionVolumeQuad2D(const dlong Nelements,
				   @restrict const  dfloat *  vgeo,
				   @restrict const  dfloat *  D,
				   @restrict const  dfloat *  fluxVector,
				   @restrict const  dfloat *  q,
				   @restrict dfloat *  rhsq){
  
  for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_D[p_Nq][p_Nq];
    @shared dfloat s_F[p_Nq][p_Nq];
    @shared dfloat s_G[p_Nq][p_Nq];
    
    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){
        s_D[j][i] = D[j*p_Nq+i];

        // geometric factors
        const dlong gbase = e*p_Np*p_dim + j*p_Nq + i;
        const dfloat Fr = fluxVector[gbase+p_Np*0];
        const dfloat Fs = fluxVector[gbase+p_Np*1];

        // conseved variables
        const dlong  qbase = e*p_Np*p_Nfields + j*p_Nq + i;
        const dfloat qn  = q[qbase+0*p_Np];

	s_F[j][i] = Fr*qn;
	s_G[j][i] = Fs*qn;
      }
    }

    @barrier("local");
    
    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){    
        const dlong gid = e*p_Np*p_Nvgeo+ j*p_Nq +i;
        const dfloat invJW = vgeo[gid + p_IJWID*p_Np];

        dfloat rhsq0 = 0, rhsq1 = 0, rhsq2 = 0;
	
        for(int n=0;n<p_Nq;++n){
          const dfloat Din = s_D[n][i];
          const dfloat Djn = s_D[n][j];
          rhsq0 += Din*s_F[j][n];
          rhsq0 += Djn*s_G[n][i];
        }
        
        const dlong base = e*p_Np*p_Nfields + j*p_Nq + i;
        
        // move to rhs (check sign)
        rhsq[base+0*p_Np] = -invJW*rhsq0;
      }
    }
  }
}


